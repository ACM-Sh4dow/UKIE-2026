#pragma kernel CSMain

Texture2D<float4>   Input;
RWTexture2D<float4> Output;
StructuredBuffer<float4> Palette;
int PaletteSize;

float3 rgb2xyz(float3 rgb)
{
    rgb = pow(abs(rgb), 2.2);
    
    float3x3 m = float3x3(
        0.4124564, 0.3575761, 0.1804375,
        0.2126729, 0.7151522, 0.0721750,
        0.01933304, 0.1192249, 0.9504300
        );
    return mul(m, rgb);
}

float3 xyz2lab(float3 xyz)
{
    xyz /= float3(0.95047, 1.0, 1.08883);
    
    float3 f = xyz > 0.008856 ? pow(xyz, 1.0 / 3.0) : (7.787 * xyz + 16.0/116.0);
    
    float L = 116.0 * f.y - 16.0;
    float a = 500.0 * (f.x - f.y);
    float b = 200.0 * (f.y - f.z);
    
    return float3(L, a, b);
}

[numthreads(8,8,1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    float4 src = Input[id.xy];

    float3 srcLab = xyz2lab(rgb2xyz(src.rgb));

    float bestDist = 1e9;
    float3 bestColor = src.rgb;

    for (int i = 0; i < PaletteSize; i++)
    {
        float3 palLab = xyz2lab(rgb2xyz(Palette[i].rgb));
        float3 diff = srcLab - palLab;
        float dist = dot(diff, diff);
        if (dist < bestDist)
        {
            bestDist  = dist;
            bestColor = Palette[i].rgb;
        }
    }

    Output[id.xy] = float4(bestColor, src.a);
}